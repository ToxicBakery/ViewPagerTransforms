/*
 * Copyright 2014 Toxic Bakery
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *       http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id("io.gitlab.arturbosch.detekt").version("1.0.0-RC12")
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

repositories {
    google()
    jcenter()
    mavenCentral()
}

android {
    compileSdkVersion android_target_sdk
    buildToolsVersion = android_build_tools_version

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion android_target_sdk
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            testCoverageEnabled true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }

    testOptions {
        animationsDisabled true
        unitTests {
            returnDefaultValues = true
            includeAndroidResources = true
        }
    }

    dexOptions {
        // Skip pre-dexing when running on CI
        preDexLibraries = !isCi
        javaMaxHeapSize maxDexHeap
    }
}

dependencies {
    implementation "com.android.support:support-v4:$android_support_version"
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
}

detekt {
    input = files("src/main/java")
    config = files("$projectDir/detekt.yml")
    parallel = true
    reports {
        xml.enabled = false
        html {
            enabled = true
            destination = file("build/reports/detekt/detekt.html")
        }
    }
}
check.dependsOn('detekt')

apply from: "${rootDir}/publish.gradle"

task dokkaGhPages(type: org.jetbrains.dokka.gradle.DokkaAndroidTask) {
    outputFormat = 'html'
    outputDirectory = "$buildDir/gh-pages"
    sourceDirs = files('src/main/java')
    skipEmptyPackages = true
    skipDeprecated = false
    noStdlibLink = false
}

task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaAndroidTask) {
    outputFormat = 'javadoc'
    outputDirectory = "$buildDir/javadoc"
    sourceDirs = files('src/main/java')
    skipEmptyPackages = true
    skipDeprecated = false
    noStdlibLink = false
}
